#!/usr/bin/env perl
#
# KeyNanny provides a framework to protect sensitive data on a Unix host.
# keynanny implements a command line client for
# - setting values
# - getting values
# - listing available keys
# - rendering templates
#
# Copyright (c) 2014 The CertNanny Project
#
# Licensed under the Apache License, Version 2.0 and the GNU General Public License, Version 2.0.
# See the LICENSE file for details.
#


use strict;
use warnings;

use Getopt::Long;
use Template;

use Data::Dumper;
use KeyNanny;

my %options = ();

my %config;
my $socketfile;

sub usage {
    print "Usage: ...\n";
}

sub process_template {
    my $kn        = shift;
    my $infile    = shift;
    my $outfile   = shift;
    my $variables = shift;

    my $fh;
    open( $fh, '<', $infile ) or die "Error opening template file: $!\n";
    local $/;
    my $input = <$fh>;
    close $fh;

    my $template = Template->new( {} );

    my $vars = {};
    foreach my $entry ( @{$variables} ) {
        $vars->{$entry} = $kn->get_var($entry);
    }

    $template->process( \$input, $vars, $outfile ) || die $template->error();

    return 1;
}

sub error_msg {
    my $result = shift;
    my $arg = shift;

    if (defined $arg) {
	print STDERR $arg . ' ';
    }
    return unless defined $result;
    return unless (ref $result eq 'HASH');
    return unless (defined $result->{STATUS});
    return 1 if ($result->{STATUS} eq 'OK');

    my $msg = '(' . $result->{STATUS};
    if (defined $result->{MESSAGE}) {
	$msg .= ': ' . $result->{MESSAGE};
    }
    $msg .= ')';

    print STDERR $msg . "\n";
    return 1;
}

GetOptions( \%options, 'socketfile=s', 'variable=s@', 'outfile=s', 'help' );

if ( $options{help} ) {
    usage();
    exit 0;
}

###########################################################################
# process configuration file
$socketfile = $options{socketfile};

my $kn = KeyNanny->new( { socketfile => $socketfile } ) or die "Error: $@";

my $cmd = shift;

my @variables = map { split(/,/) } @{ $options{variable} };

if ( !defined $cmd ) {
    usage;
    exit 0;
}

my $rc = 0;
if ( $cmd eq 'get' ) {
    my $arg = shift;
    if (defined $arg) {
	my $result = $kn->get_var($arg);

	if ($result->{STATUS} eq 'OK') {
	    print $result->{DATA};
	} else {
	    error_msg($result, 'Could not get key');
	    $rc = 1;
	}
    } else {
	print STDERR "Missing argument for get operation\n";
	$rc = 1;
    }
} elsif ( $cmd eq 'set' ) {
    my $arg = shift;
    my $value = shift;

    if (! defined $value) {
	print STDERR "No value specified (use - for STDIN)\n";
	$rc = 1;
    } elsif ($value eq '-') {
	local $/;
	$value = <STDIN>;
    }
    if (defined $value) {
	my $result = $kn->set_var($arg, $value);
	if ($result->{STATUS} ne 'OK') {
	    error_msg($result, 'Could not set key');
	    $rc = 1;
	}
    }
} elsif ( $cmd eq 'list' ) {
    my $result = $kn->list_vars();
    if ($result->{STATUS} eq 'OK') {
	print join("\n", @{$result->{KEYS}}) . "\n";
    } else {
	error_msg($result, "Could not get list of keys");
	$rc = 1;
    }
} elsif ( $cmd eq 'template' ) {
    my $infile = shift;

    if (scalar @variables == 0) {
	my $result = $kn->list_vars();
	if ($result->{STATUS} eq 'OK') {
	    @variables = @{$result->{KEYS}};
	}
    }
    
    if ( !process_template( $kn, $infile, $options{outfile}, \@variables ) ) {
        $rc = 1;
    }

}
else {
    die "Invalid command '$cmd'. Stopped";
}

exit $rc;
